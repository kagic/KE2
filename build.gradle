buildscript {
    repositories {
        maven { url = "http://files.minecraftforge.net/maven" }
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = new Date().format('yy.MM.dd')
group = "mod.ke2"
archivesBaseName = "KAGIC"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

minecraft {
    replace "@version", project.version
    version = "1.12.2-14.23.5.2814"
    runDir = "run"
    mappings = "snapshot_20171003"
}
processResources {
    inputs.property "version", project.version
    from (sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version
    }
    from (sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
compileJava {
    sourceCompatibility = project.sourceCompatibility
    targetCompatibility = project.targetCompatibility
}
configurations {
	customCompile
}
dependencies {
	customCompile fileTree(dir: 'lib', includes: ['*.jar'])
	compile configurations.customCompile
}
jar {
	from sourceSets.api.output
	from (
		configurations.customCompile.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	) { exclude 'META-INF/**' }
}
task deobf(type: Jar) {
	from sourceSets.main.output
	from sourceSets.api.output
	appendix = "deobf"
}
artifacts {
	archives deobf
}
